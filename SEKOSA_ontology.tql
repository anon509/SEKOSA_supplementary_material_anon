#############################################################################
#                                                                           #
#     	Software Engineering Knowledge Ontology for Self-Assessment 	    #
#                                                                           # 
#############################################################################
define

################
## Attributes ##
################

description sub attribute, value string;
distance sub attribute, value double;
diagonal sub attribute, value double;

name sub attribute, value string;

health_state sub attribute, value boolean;

property sub attribute, abstract, value double,
    plays featuring:feature;

    rate sub property, abstract;
        fps sub rate;
    capacity sub property;
    format sub property, abstract;
        ROS_message sub format;
    throughput sub property, abstract;
        power sub throughput;
    unit sub property, abstract;
        wavelength sub unit;
        intensity sub unit;
        voltage sub unit;
        m_s sub unit;
    location sub property, abstract;
        ROS_topic sub location;
    quantifier sub property, abstract;
        minimal sub quantifier;
        exact sub quantifier;
        maximal sub quantifier;

#properties that have no value double.
quality sub attribute, abstract, value double;
 

    average_quality sub attribute, abstract, value double;
        average_lumen sub average_quality;
        average_decibel sub average_quality;
        average_contrast sub average_quality;
        average_change_correct_detection sub average_quality;
        average_signal_to_noise sub average_quality;
        average_error_distance sub average_quality;
            average_error_distance_robot sub average_quality;
            average_error_distance_acoustic sub average_quality;
            average_error_distance_fast sub average_quality;
            average_error_distance_thorough sub average_quality;
        
        minimal_allowed_change_correct_detection sub average_quality;
        minimal_allowed_error_distance sub average_quality;

    minimal_quality sub attribute, abstract, value double;
        min_lumen sub minimal_quality,
            plays featuring:feature;
        min_decibel sub minimal_quality,
            plays featuring:feature;
        min_contrast sub minimal_quality,
            plays featuring:feature;
        min_change_correct_detection sub minimal_quality,
            plays featuring:feature;
        min_signal_to_noise sub minimal_quality,
            plays featuring:feature;
        min_error_distance sub minimal_quality,
            plays featuring:feature;
        min_error_distance_robot sub minimal_quality,
            plays featuring:feature;


    maximal_quality sub attribute, abstract, value double;
        max_lumen sub maximal_quality,
            plays featuring:feature;
        max_decibel sub maximal_quality,
            plays featuring:feature;
        max_contrast sub maximal_quality,
            plays featuring:feature;
        max_change_correct_detection sub maximal_quality,
            plays featuring:feature;
        max_signal_to_noise sub maximal_quality,
            plays featuring:feature;
        max_error_distance sub maximal_quality,
            plays featuring:feature;
        max_error_distance_robot sub maximal_quality,
            plays featuring:feature;


size sub attribute, abstract, value double; #changed to double instead of string.
    min_size sub size; 
    max_size sub size;
    
performance sub attribute, abstract, value double, #specify any type of performance
    plays featuring:feature;

######################
## ROLES & RELATION ##
######################
processing sub relation,
    relates input, # any creation
    relates executor, # component in question
    relates output, # any creation
    relates context, #sensing_context
    relates why, #in TO
    plays processing_requirement:need;
    #relates requested_behaviour, #in TO
    #owns planned_status; # in TO

requirement sub relation,
    relates call,
    relates petitioner,
    relates product;

    functional_requirement sub requirement,
        relates input as call, # data
        relates output as product;  # data

    non_functional_requirement sub requirement,
        relates service as call, # resource
        relates outcome as product; # data

    environmental_requirement sub requirement, 
        relates state as call, # physical quantity
        relates outcome as product; # data

    processing_requirement sub requirement,
        relates need as call,
        relates output as product;

realizing sub relation, 
    relates provider, # any creation
    relates requester;    # any creation

featuring sub relation,
    relates subject,
    relates feature,
    plays requirement:call,
    plays environmental_requirement:state,
    plays non_functional_requirement:service,
    plays functional_requirement:input;

sensing_context sub relation,
    owns description,
    relates physical_context,
    plays processing:context;

##############
## ENTITIES ##
##############

Creation sub entity, abstract,
    owns description,
    plays realizing:provider,
    plays realizing:requester,
    plays processing:input,
    plays processing:output;

    Data sub Creation;

    RGBImage sub Data,
        owns min_contrast,
        owns max_contrast,
        owns average_contrast,
        plays functional_requirement:input,
        plays environmental_requirement:outcome,
        plays featuring:subject,
        plays processing_requirement:output;
    
    Detection sub Data,
        owns min_change_correct_detection,
        owns max_change_correct_detection,
        owns average_change_correct_detection,
        plays functional_requirement:input,
        plays functional_requirement:output,
        plays featuring:subject,
        plays processing_requirement:output;


    VictimPose sub Data,
        owns min_error_distance,
        owns max_error_distance,
        owns average_error_distance_fast,
        owns average_error_distance_acoustic,
        owns average_error_distance_thorough,
        plays functional_requirement:input,
        plays functional_requirement:output,
        plays featuring:subject,
        plays processing_requirement:output;

    TextToSpeech sub Data,
        owns min_signal_to_noise,
        owns max_signal_to_noise,
        owns average_signal_to_noise,
        plays environmental_requirement:outcome,
        plays functional_requirement:input,
        plays featuring:subject,
        plays processing_requirement:output;

        Signal sub Data,
            owns wavelength, #TODO: check of all deze properties kloppen (bij alle niet abstract creations)
            owns intensity,
            owns voltage,
            owns fps;
        Information sub Data,
            owns ROS_topic,
            owns ROS_message;
        Knowledge sub Data,
            owns ROS_topic,
            owns ROS_message;

    Resource sub Creation;

        ElectricPower sub Resource,
            owns power,
            owns voltage;
        ComputingPower sub Resource,
            owns power,
            owns voltage;

PhysicalConcept sub entity, abstract,
    owns description,
    plays environmental_requirement:state,
    plays realizing:provider,
    plays realizing:requester,
    plays processing:input;

        LightIntensity sub PhysicalConcept,
            owns min_lumen,
            owns max_lumen,
            owns average_lumen,
            plays sensing_context:physical_context,
            plays featuring:subject;
            # plays environmental_requirement:state;

        AmbientNoise sub PhysicalConcept,
            owns min_decibel,
            owns max_decibel,
            owns average_decibel,
            plays sensing_context:physical_context,
            plays featuring:subject;

        RoomSize sub PhysicalConcept,
            owns min_size,
            owns max_size,
            owns diagonal,
            plays sensing_context:physical_context;
        
        WayPointDistance sub PhysicalConcept,
            owns distance,
            plays sensing_context:physical_context;

        RobotPose sub PhysicalConcept,
            owns min_error_distance_robot,
            owns max_error_distance_robot,
            owns average_error_distance_robot,
            plays functional_requirement:input,
            plays functional_requirement:output,
            plays featuring:subject,
            plays processing_requirement:output;


   PhysicalQuantity sub PhysicalConcept,
        plays functional_requirement:output;

        Light sub PhysicalQuantity,
            owns intensity,
            owns average_lumen;
        Speed sub PhysicalQuantity,
            owns m_s;
        Sound sub PhysicalQuantity,
            owns average_decibel;

    PhysicalState sub PhysicalConcept;

Component sub entity,
    owns description,
    owns health_state,
    plays requirement:petitioner,
    plays processing:executor;

    Sensor sub Component;
        Camera sub Sensor;
        Micro sub Sensor;
    Actuator sub Component;
    FunctionalComponent sub Component;
        Detector sub FunctionalComponent;
        NLP sub FunctionalComponent;
        SpeechToText sub FunctionalComponent;
        ObjectTracking sub FunctionalComponent;
    ApplianceComponent sub Component;

Behaviour sub entity, 
    owns description,
    owns name,
    owns health_state, #set by rule (instead of valid)
    plays requirement:petitioner;

###########
## rules ##
###########

rule realizing_external:
    when {
        $input isa PhysicalConcept, has average_quality $att, has description $d1;
        $rq_input isa PhysicalConcept, has description $d2;
        $d1 = $d2;
        $rq_min_attr isa minimal_quality;
        $rq_max_attr isa maximal_quality;
        (featuring: subject: $rq_input, feature: $rq_min_attr) isa featuring;
        (featuring: subject: $rq_input, feature: $rq_max_attr) isa featuring;
        $att <= $rq_max_attr;
        $att >= $rq_min_attr;
    } then {
        (realizing: provider: $input, requester: $rq_input) isa realizing;
    };


rule realizing_rule:
    when {
        $input isa Creation, has average_quality $att, has description $d1;
        $rq_input isa Creation, has description $d2;
        $d1 = $d2;
        $rq_min_attr isa minimal_quality;
        $rq_max_attr isa maximal_quality;
        (featuring: subject: $rq_input, feature: $rq_min_attr) isa featuring;
        (featuring: subject: $rq_input, feature: $rq_max_attr) isa featuring;
        $att <= $rq_max_attr;
        $att >= $rq_min_attr;
    } then {
        (realizing: provider: $input, requester: $rq_input) isa realizing;
    };



rule processing_external:
    when {
        $data_rel isa PhysicalConcept;
        $data_req isa PhysicalConcept;
        $feature_req (featuring: subject: $data_req) isa featuring;
        $data_out isa Creation;
        $comp isa Component;
        $rq (requirement: call: $feature_req, petitioner: $comp, product: $data_out) isa requirement;
        $real (realizing: provider: $data_rel, requester: $data_req) isa realizing;
    } then {
        (processing: input: $data_rel, executor: $comp, output: $data_out) isa processing;
    };


rule processing_rule:
    when {
        $data_rel isa Creation;
        $data_req isa Creation;
        $feature_req (featuring: subject: $data_req) isa featuring;
        $data_out isa Creation;
        $comp isa Component;
        $rq (requirement: call: $feature_req, petitioner: $comp, product: $data_out) isa requirement;
        $real (realizing: provider: $data_rel, requester: $data_req) isa realizing;
        $pro (processing: output: $data_rel) isa processing;
    } then {
        (processing: input: $data_rel, executor: $comp, output: $data_out) isa processing;
    };


rule processing_is_transitive2:
    when {
        (processing: input: $data_in, executor: $comp1, output: $data_out) isa processing;
        (processing: input: $data_out, executor: $comp2, output: $data_out2) isa processing;
    } then {
        (processing: input: $data_in, executor: $comp1, executor: $comp2, output: $data_out2) isa processing;
    };


# rule processing_is_transitive3:
#     when {
#         (processing: input: $data_in, executor: $comp1, output: $data_out) isa processing;
#         (processing: input: $data_out, executor: $comp2, output: $data_out2) isa processing;
#         (processing: input: $data_out2, executor: $comp3, output: $data_out3) isa processing;
#     } then {
#         (processing: input: $data_in, executor: $comp1, executor: $comp2, executor: $comp3, output: $data_out3) isa processing;
#     };


# behaviour requirements - NIET MEER AANGEPAST WANT NIET MEER NODIG; TOCH?!
# rule search_behaviour_requirement:
#     when {
#         $beh isa Behaviour, has name "Undefined Search";
#         $proc (processing: input: $inp, output: $pose) isa processing;
#         $inp isa PhysicalConcept;
#         $pose isa Creation, has average_quality $quality;
#         $quality > 0.1;
#     } then {
#         (processing_requirement: need: $proc, petitioner: $beh, output: $pose) isa processing_requirement;
#     };


rule visual_fast_search_behaviour_requirement:
    when {
        $beh isa Behaviour, has name "Visual Fast Search";
        $proc1 (processing: input: $light, executor: $cam, output: $detection) isa processing;
        $light isa LightIntensity;
        $detection isa Creation, has average_change_correct_detection $quality_change_correct_detection;
        $proc2 (processing: input: $robpos, executor: $track, output: $vicpose) isa processing;
        $proc3 (processing: input: $detection, executor: $track, output: $vicpose) isa processing;
        $track isa ObjectTracking;
        $robpos isa RobotPose;
        # $rel (realizing: provider: $detection, requester: $track) isa realizing;
        $vicpose isa VictimPose, has average_error_distance_fast $quality_error_distance_fast;
        $minimal_allowed_quality_change_correct_detection isa minimal_allowed_change_correct_detection;
        $minimal_allowed_quality_error_distance isa minimal_allowed_error_distance;
        $quality_change_correct_detection > $minimal_allowed_quality_change_correct_detection;
        $quality_error_distance_fast > $minimal_allowed_quality_error_distance;
    } then {
        (processing_requirement: need: $proc1, need: $proc2, petitioner: $beh, output: $detection, output: $vicpose) isa processing_requirement;
    };


rule visual_thorough_search_behaviour_requirement:
    when {
        $beh isa Behaviour, has name "Visual Thorough Search";
        $proc1 (processing: input: $light, executor: $cam, output: $detection) isa processing;
        $light isa LightIntensity;
        $detection isa Creation, has average_change_correct_detection $quality_change_correct_detection;
        $proc2 (processing: input: $robpos, executor: $track, output: $vicpose) isa processing;
        $proc3 (processing: input: $detection, executor: $track, output: $vicpose) isa processing;
        $track isa ObjectTracking;
        $robpos isa RobotPose;
        # $rel (realizing: provider: $detection, requester: $track) isa realizing;
        $vicpose isa VictimPose, has average_error_distance_thorough $quality_error_distance_thorough;
        $minimal_allowed_quality_change_correct_detection isa minimal_allowed_change_correct_detection;
        $minimal_allowed_quality_error_distance isa minimal_allowed_error_distance;
        $quality_change_correct_detection > $minimal_allowed_quality_change_correct_detection;
        $quality_error_distance_thorough > $minimal_allowed_quality_error_distance;
    } then {
        (processing_requirement: need: $proc1, need: $proc2, petitioner: $beh, output: $detection, output: $vicpose) isa processing_requirement;
    };

rule acoustic_search_behaviour_requirement:
    when {
        $beh isa Behaviour, has name "Acoustic Search";
        $proc1 (processing: input: $inp, executor: $NLP, output: $detection) isa processing;
        $inp isa PhysicalConcept;
        $NLP isa NLP;
        $detection isa Creation, has average_change_correct_detection $quality_change_correct_detection;
        $proc2 (processing: input: $inp, executor: $speechtotext, executor: $NLP, output: $vicpose) isa processing;
        $speechtotext isa SpeechToText;
        $vicpose isa VictimPose, has average_error_distance_acoustic $quality_error_distance_acoustic;
        $minimal_allowed_quality_change_correct_detection isa minimal_allowed_change_correct_detection;
        $minimal_allowed_quality_error_distance isa minimal_allowed_error_distance;
        $quality_change_correct_detection > $minimal_allowed_quality_change_correct_detection;
        $quality_error_distance_acoustic > $minimal_allowed_quality_error_distance;
    } then {
        (processing_requirement: need: $proc1, need: $proc2, petitioner: $beh, output: $detection, output: $vicpose) isa processing_requirement;
    };
